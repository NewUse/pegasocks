#
# found in on openssl site and made some changes GPL 2.0 licenced

IF(NOT WITH_LIBEVENT)
  SET(WITH_LIBEVENT system)
ENDIF()

IF(WITH_LIBEVENT STREQUAL "system" OR WITH_LIBEVENT STREQUAL "yes")
  IF(NOT WIN32)
    SET(LIBEVENT2_INCLUDE_PATH /usr/local/include /opt/local/include)
    SET(LIBEVENT2_LIB_PATHS /usr/local/lib /opt/local/lib)
  ENDIF()

  # use default paths
  SET(HOW_TO_FIND)
ELSEIF(WITH_LIBEVENT STREQUAL "bundled")
  MESSAGE(FATAL_ERROR "bundled libevent isn't support")
ELSE()
  # make the users path for libevent absolute
  GET_FILENAME_COMPONENT(LIBEVENT_ABS_DIR "${WITH_LIBEVENT}" ABSOLUTE)
  SET(LIBEVENT2_INCLUDE_PATH ${LIBEVENT_ABS_DIR}/include)
  SET(LIBEVENT2_LIB_PATHS ${LIBEVENT_ABS_DIR}/lib)

  # if path specified, use that path only
  SET(HOW_TO_FIND NO_DEFAULT_PATH)
ENDIF()

FIND_PATH(LIBEVENT2_INCLUDE_DIR event2/event.h PATHS ${LIBEVENT2_INCLUDE_PATH} ${HOW_TO_FIND})
IF(WIN32)
  ## libevent-2.0.22 on windows is only 'event.lib' and 'event.dll'
  FIND_LIBRARY(LIBEVENT2_CORE NAMES event PATHS ${LIBEVENT2_LIB_PATHS} ${HOW_TO_FIND})
  SET(LIBEVENT2_EXTRA)
ELSE()
  FIND_LIBRARY(LIBEVENT2_CORE NAMES event_core libevent_core PATHS ${LIBEVENT2_LIB_PATHS} ${HOW_TO_FIND})
  FIND_LIBRARY(LIBEVENT2_EXTRA NAMES event_extra libevent_extra PATHS ${LIBEVENT2_LIB_PATHS} ${HOW_TO_FIND})
  FIND_LIBRARY(LIBEVENT2_SSL_LIBRARIES NAMES event_openssl libevent_openssl PATHS ${LIBEVENT2_LIB_PATHS} ${HOW_TO_FIND})
  FIND_LIBRARY(LIBEVENT2_MBEDTLS_LIBRARIES NAMES event_mbedtls libevent_mbedtls PATHS ${LIBEVENT2_LIB_PATHS} ${HOW_TO_FIND})
  FIND_LIBRARY(LIBEVENT2_PTHREADS_LIBRARIES NAMES event_pthreads libevent_pthreads PATHS ${LIBEVENT2_LIB_PATHS} ${HOW_TO_FIND})
ENDIF()

IF (LIBEVENT2_INCLUDE_DIR AND LIBEVENT2_CORE)
  SET(LibEvent2_FOUND TRUE)
ELSE()
  SET(LibEvent2_FOUND FALSE)
ENDIF()

# don't expose them in the cmake UI
MARK_AS_ADVANCED(
  LIBEVENT2_INCLUDE_DIR
  LIBEVENT2_CORE
  LIBEVENT2_EXTRA
  LIBEVENT2_SSL_LIBRARIES
  LIBEVENT2_MBEDTLS_LIBRARIES
  LIBEVENT2_PTHREADS_LIBRARIES
)
